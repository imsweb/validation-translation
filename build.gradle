import com.vanniktech.maven.publish.*

plugins {
    id 'java-library'
    id 'com.vanniktech.maven.publish' version '0.36.0' // publish to Maven Central
    id 'com.github.ben-manes.versions' version '0.53.0' // check for out-of-date dependencies (run 'dependencyUpdates' manually)
    id 'com.github.spotbugs' version '6.4.8' // spotbugs code analysis
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'This library allows a Genedits Metafile to be translated into Groovy edits that can be executed in the SEER Validation framework.'

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.imsweb:validation:4.8'
    api 'com.imsweb:naaccr-xml:12.1'
    api 'com.imsweb:seerutils:5.7'
    api 'com.imsweb:staging-algorithm-cs:02.05.50.10'

    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'net.sf.squirrel-sql.thirdparty-non-maven:java-cup:0.11a'
    implementation 'org.xerial:sqlite-jdbc:3.51.1.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.25.3'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.imsweb:data-generator:2.3'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.25.3'
}

// enforce UTF-8, display the compilation warnings
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
    options.addBooleanOption('html5', true)
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties.getProperty('java.vm.version') + ' (' + System.properties.getProperty('java.vm.vendor') + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.validation-translation'
        )
    }
}

// spotbugs plugin settings
spotbugs {
    excludeFilter.set(file('config/spotbugs/spotbugs-exclude.xml'))
}

// use this task to generate the edits parser for Genedits syntax
tasks.register('generateEditsLexer', JavaExec) {
    group = 'parser'
    classpath = files('config/jflex-1.4.2/lib/JFlex.jar')
    args = ['--nobak', '-d', 'src/test/java/com/imsweb/seeredits/genedits/language', 'src/test/resources/syntax/genedits.flex']
}

// use this task to generate the edits parser for Genedits syntax
tasks.register('generateEditsParser', JavaExec) {
    group = 'parser'
    classpath = files('config/jflex-1.4.2/lib/java-cup-11a.jar')
    args = ['-symbols', 'GeneditsTokenType', '-parser', 'GeneditsParser', '-destdir', 'src/test/java/com/imsweb/seeredits/genedits/language', 'src/test/resources/syntax/genedits.cup']
}

// use this task to generate the regex lexer
tasks.register('generateRegexLexer', JavaExec) {
    group = 'parser'
    classpath = files('config/jflex-1.4.2/lib/JFlex.jar')
    args = ['--nobak', '-d', 'src/test/java/com/imsweb/seeredits/genedits/language/regex', 'src/test/resources/syntax/regex.flex']
}

// use this task to generate the regex parser
tasks.register('generateRegexParser', JavaExec) {
    group = 'parser'
    classpath = files('config/jflex-1.4.2/lib/java-cup-11a.jar')
    args = ['-symbols', 'GeneditsRegexToken', '-parser', 'GeneditsRegexParser', '-destdir', 'src/test/java/com/imsweb/seeredits/genedits/language/regex', 'src/test/resources/syntax/regex.cup']
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return (!stableKeyword && !(version ==~ regex)) || version == '021-11'
}

// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

// needed to deploy to Maven Central Portal
mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), new SourcesJar.Sources()))

    publishToMavenCentral(true)
    signAllPublications()

    pom {
        name = 'Validation Translation'
        description = 'This project allows a Genedits Metafile to be translated into Groovy edits that can be executed in the SEER Validation framework.'
        url = 'https://github.com/imsweb/validation-translation'
        inceptionYear = '2021'

        licenses {
            license {
                name = 'A modified BSD License (BSD)'
                url = 'https://github.com/imsweb/validation/blob/master/LICENSE'
                distribution = 'repo'
            }
        }

        developers {
            developer {
                id = 'depryf'
                name = 'Fabian Depry'
                email = 'depryf@imsweb.com'
            }
        }

        scm {
            url = 'https://github.com/imsweb/validation-translation'
            connection = 'scm:https://github.com/imsweb/validation-translation.git'
            developerConnection = 'scm:git@github.com:imsweb/validation-translation.git'
        }
    }
}

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '9.2.1'
    distributionType = Wrapper.DistributionType.ALL
}


